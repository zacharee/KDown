@file:Suppress("UnstableApiUsage")

import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.targets.native.tasks.KotlinNativeSimulatorTest

plugins {
  alias(libs.plugins.kotlinMultiplatform)
  alias(libs.plugins.androidKmpLibrary)
  alias(libs.plugins.kotlinx.serialization)
  alias(libs.plugins.mavenPublish)
}

kotlin {
  compilerOptions {
    optIn.addAll(
      "kotlin.uuid.ExperimentalUuidApi",
      "kotlin.time.ExperimentalTime",
      "kotlinx.coroutines.ExperimentalCoroutinesApi",
    )
  }

  android {
    namespace = "com.linroid.ketch.core"
    compileSdk = libs.versions.android.compileSdk.get().toInt()
    minSdk = libs.versions.android.minSdk.get().toInt()

    compilerOptions {
      jvmTarget.set(JvmTarget.JVM_11)
    }
    optimization {
      consumerKeepRules.apply {
        publish = true
        file("consumer-rules.pro")
      }
    }
  }

  iosArm64()
  iosSimulatorArm64()

  jvm()

  js {
    nodejs()
  }

  // @OptIn(ExperimentalWasmDsl::class)
  // wasmWasi {
  //   nodejs()
  // }

  sourceSets {
    commonMain.dependencies {
      api(projects.library.api)
      implementation(libs.kotlinx.coroutines.core)
      implementation(libs.kotlinx.serialization.json)
      implementation(libs.kotlinx.datetime)
      implementation(libs.okio)
    }
    commonTest.dependencies {
      implementation(libs.kotlin.test)
      implementation(libs.kotlinx.coroutines.test)
    }
    androidMain.dependencies {
      implementation(libs.androidx.startup)
    }
    jsMain.dependencies {
      implementation(libs.okio.nodefilesystem)
    }
    // wasmWasiMain.dependencies {
    //   implementation(libs.okio.wasifilesystem)
    // }
  }
}

tasks.withType<KotlinNativeSimulatorTest>().configureEach {
  // Some environments have Xcode CLI tools but no arm64 simulator SDK support.
  // Keep regular builds green by requiring explicit opt-in for simulator test execution.
  enabled = providers.gradleProperty("enableIosSimulatorTests").orNull == "true"
}
